#!/usr/bin/env perl

#  runtest -- run regression tests
#  Copyright (C) 2002-2014 Dieter Baron and Thomas Klausner
#
#  This file is part of ckmame, a program to check rom sets for MAME.
#  The authors can be contacted at <ckmame@nih.at>
#
#  Redistribution and use in source and binary forms, with or without
#  modification, are permitted provided that the following conditions
#  are met:
#  1. Redistributions of source code must retain the above copyright
#     notice, this list of conditions and the following disclaimer.
#  2. Redistributions in binary form must reproduce the above copyright
#     notice, this list of conditions and the following disclaimer in
#     the documentation and/or other materials provided with the
#     distribution.
#  3. The names of the authors may not be used to endorse or promote
#     products derived from this software without specific prior
#     written permission.
# 
#  THIS SOFTWARE IS PROVIDED BY THE AUTHORS ``AS IS'' AND ANY EXPRESS
#  OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
#  WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
#  ARE DISCLAIMED.  IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY
#  DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
#  DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE
#  GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
#  INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER
#  IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
#  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN
#  IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.


use strict;

#use Data::Dumper qw(Dumper);

BEGIN { push @INC, '@abs_srcdir@'; }

use CkmameDB;
use NiHTest;

my $ROMDIRS = "extra|extradir|moreroms|needed|roms|search|unknown";

my $test = NiHTest::new({ default_program => 'ckmame', srcdir => '@srcdir@', top_builddir => '@top_builddir@', zipcmp_flags => '' });

sub check_ckmame_db {
	my ($test, $dir, $variant) = @_;
	
	my $dump_expected = undef;
	
	if (defined($test->{test}->{'ckmamedb-after'})) {
		for my $args (@{$test->{test}->{'ckmamedb-after'}}) {
			if ($args->[0] eq $dir) {
				my $dumpfile = $test->find_file($args->[1]);
				return undef unless ($dumpfile);
				
				my $fh;
				unless (open($fh, "< $dumpfile")) {
					print STDERR "can't open $dumpfile: $!";
					return undef;
				}
				$dump_expected = [ map { chomp; $_; } <$fh> ];
				close($fh);
				last;
			}
		}
	}
	
	my $db = new CkmameDB($dir, undef, $variant eq 'dir', $test->{test}->{'no-hashes'});
	
	if (!$db) {
		print "opening $dir/.ckmame.db failed\n" if ($test->{verbose});
		return undef;
	}

	return undef unless ($db->read_db());
	if (!defined($dump_expected)) {
		return undef unless ($db->read_archives() && $db->make_dump());
		$dump_expected = $db->{dump_expected};
	}
	
	return $test->compare_arrays($dump_expected, $db->{dump_got}, "$dir/.ckmame.db");
}


sub checks {
	my ($test, $hook, $variant) = @_;

	my %checked = ();
	
	my $ok = 1;
	my $error = 0;

	my $dir;
	unless (opendir($dir, '.')) {
	    print STDERR "cannot list '.': $!\n";
	    return undef;
	}

	while (my $name = readdir $dir) {
	    next if ($name eq '.' || $name eq '..');
	    next unless (-d $name);

	    my $ret = check_ckmame_db($test, $name, $variant);
	    $checked{$name} = 1;
	    
	    if (!defined($ret)) {
		$error = 1;
		next;
	    }
	    elsif ($ret != 1) {
		$ok = 0;
	    }
	}
	closedir $dir;

	if (defined($test->{test}->{'ckmamedb-after'})) {
	    for my $entry (@{$test->{test}->{'ckmamedb-after'}}) {
		my $name = $entry->[0];
		next if ($checked{$name});
		
		my $ret = check_ckmame_db($test, $name, $variant);
		$checked{$name} = 1;
		
		if (!defined($ret)) {
		    return undef;
		}
		elsif ($ret != 1) {
		    $ok = 0;
		}
	    }
	}
	    
	push @{$test->{failed}}, 'ckmame.db' unless ($ok);
	
	return $error ? undef : 1;
}

sub comparator_db {
	my ($test, $got, $expected) = @_;
	# $got -- db
	# $expected -- db dump

	my $cmd = "../dbdump \"$got\" | diff " . ($test->{verbose} ? '-u' : '-q') . " \"$expected\" -";
	my $ret = system($cmd);
    
	return $ret == 0;
}

sub dir_comparator_zip {
	my ($test, $got, $expected) = @_;

	$got =~ s/\.zip//;
	return NiHTest::comparator_zip($test, $got, $expected);
}


sub dir_copier_zip {
	my ($test, $src, $dest) = @_;

	$dest =~ s/\.zip$//;

	return system('unzip', '-qd', $dest, $src) == 0;
}


sub mangle_program {
	my ($test, $hook) = @_;

	$test->{test}->{program} =  "../src/$test->{test}->{program}";

	return 1;
}


sub post_list_files {
	my ($test, $hook, $variant) = @_;

	my %got = ();

	#print(Dumper(\$test));
	for my $file (@{$test->{files_got}}) {
		next if ($file =~ m,/.ckmame.db$,);
		next if ($file =~ m,($ROMDIRS)/$,o);
		next if ($file =~ m,extra/foo/$,); # TODO: add empty dir directive to NiHTest, move to test case.
		if ($variant eq 'dir') {
			if ($file =~ m,^($ROMDIRS)/([^/]*)/,o) {
				$got{"$1/$2.zip"} = 1;
				next;
			}
		}

		$got{$file} = 1;
	}
	$test->{files_got} = [sort keys %got];
	#print(Dumper(\$test));
	return 1;
}


sub post_parse {
	my ($test, $hook) = @_;

	if (!defined($test->{test}->{variants})) {
		if ($test->{test}->{program} !~ m/ckmame$/) {
			$test->{test}->{variants} = [ 'zip' ];
		}
	}

	if ($test->{test}->{mkdbargs}) {
		$test->add_file({ destination => 'mame.db', ignore => 1});
	}
	else {
		$test->add_file({ source => 'mame.db', destination => 'mame.db', result => 'mame.db'});
	}

	return 1;
}


sub post_copy_file {
	my ($test, $hook) = @_;
	
	if ($test->{test}->{mkdbargs}) {
		my $ret = system('../../src/mkmamedb', @{$test->{test}->{mkdbargs}});
		# TODO: capture stdout/stderr
		return $ret == 0 ? 1 : undef;
	}
	if (! -d 'roms') {
		mkdir('roms');
	}

	if (defined($test->{test}->{'ckmamedb-before'})) {
		for my $args (@{$test->{test}->{'ckmamedb-before'}}) {
			my ($dir, $dump) = @$args;
			my $dump_file = $test->find_file($dump);
			return undef unless (defined($dump));

		        if (! -d $dir) {
			        mkdir($dir);
		        }
			unless (system('../dbrestore', '-t', 'ckmamedb', "$dump_file", "$dir/.ckmame.db") == 0) {
				print STDERR "can't restore ckmamedb dump $dump: $!";
				return undef;
			}
		}
	}

	return 1;
}


$test->add_directive(mkdbargs => { type => 'string...', once => 1 });
$test->add_directive('ckmamedb-before' => { type => 'string string' });
$test->add_directive('ckmamedb-after' => { type => 'string string' });
$test->add_directive('no-hashes' => {
	type => 'string string string? string?',
	usage => 'directory archive [file] [hash-types]',
	description => 'Specify that certain hashes are missing from cachedb. If HASH-TYPES is omitted, only crc is present; if FILE is omitted, it applies to all files from ARCHIVE.'
});

$test->add_comparator('db/dump', \&comparator_db);
$test->add_comparator('dir/zip/zip', \&dir_comparator_zip);
$test->add_comparator('zip/zip', \&NiHTest::comparator_zip);
$test->add_copier('dir/zip/zip', \&dir_copier_zip);

$test->add_hook('checks', \&checks);
$test->add_hook('mangle_program', \&mangle_program);
$test->add_hook('post_parse', \&post_parse);
$test->add_hook('post_list_files', \&post_list_files);
$test->add_hook('post_copy_files', \&post_copy_file);

sub dir_mangle_test {
    my ($test, $variant) = @_;

    map { s,((?:$ROMDIRS)/[^ :]*)\.zip,$1,go; } @{$test->{test}->{stdout}};

    if ($test->{test}->{program} =~ m/(ckmame|mamedb)$/) {
	unshift @{$test->{test}->{args}}, '-u';
    }

    return 1;
}

$test->add_variant('zip', { });
$test->add_variant('dir', { mangle_test => \&dir_mangle_test });

$test->run(@ARGV);
